plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.jetbrains.kotlin.android)
}

android {
    namespace 'com.arxlibertatis'
    compileSdk versions.target_sdk

    defaultConfig {
        applicationId "com.arxlibertatis"
        minSdk versions.min_sdk
        targetSdk versions.target_sdk
        versionCode versions.app_version_code
        versionName versions.app_version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        ndk {
            abiFilters = []
            abiFilters.addAll(abi_filters)
        }
        externalNativeBuild {
            cmake {
                arguments"-DANDROID_PLATFORM=android-$versions.native_android_platform_version",
                        "-DCMAKE_C_FLAGS_RELEASE=-O3 -fno-delete-null-pointer-checks -DNDEBUG",
                        "-DCMAKE_CXX_FLAGS_RELEASE=-O3 -fno-delete-null-pointer-checks -DNDEBUG"
                cFlags "-Wl,--strip-all" , "-ffast-math" , "-Wno-error=format-security"
                cppFlags  "-Wl,--strip-all" , "-ffast-math" , "-fexceptions", "-frtti" ,"-Wno-error=format-security"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "$rootProject.rootDir/CMakeLists.txt"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.1'
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    buildToolsVersion versions.build_tools_version
    ndkVersion versions.ndk_version
}

preBuild.dependsOn ":libopenal:build", ":libepoxy:build", ":freetype2:build" ,
        ":glm:build",":zlib:build",':sdl2:android-project:app:build'

dependencies {
    implementation project(':libepoxy')
    implementation project(':libopenal')
    implementation project(':freetype2')
    implementation project(':glm')
    implementation project(':zlib')
    implementation project(':sdl2:android-project:app')
    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.ui.test.junit4
    debugImplementation libs.androidx.ui.tooling
    debugImplementation libs.androidx.ui.test.manifest
}