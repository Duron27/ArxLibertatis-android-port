apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.bugsnag.android.gradle'

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('release-keystore.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

ext {
    versionFile = new File(project.rootDir, 'app/src/main/assets/libopenmw/resources/version')
    calculateVersion = {
        stream = new FileInputStream(versionFile)
        return stream.readLines()[0].trim() + "-" + project.android.defaultConfig.versionCode.toString()
    }
}

android {
    compileSdkVersion 29

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    sourceSets {
        main {
            resources {
                srcDir {
                    // To include the wrap.sh script required by ASAN
                    "wrap/res"
                }
            }
        }
    }

    defaultConfig {
        applicationId "is.cavebros.omw"
        versionCode 49
        versionName calculateVersion()

        minSdkVersion 21
        // Do not update past 29 -- see https://github.com/xyzz/openmw-android/issues/30
        targetSdkVersion 29

        buildConfigField("int", "RANDOMIZER", Math.abs(new Random().nextInt(999)).toString())
        ndk {
            abiFilters "arm64-v8a", "x86", "x86_64"
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            debuggable false
        }
    }

    flavorDimensions "version"

    productFlavors {
        mainline {
            dimension "version"
        }

        nightly {
            dimension "version"
            applicationId "is.cavebros.omw_nightly"
            versionNameSuffix "-nightly"
        }
    }
    ndkVersion"26.3.11579264"

    
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "omw_${variant.buildType.name}_${defaultConfig.versionName}.apk"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.anko:anko-sqlite:$anko_version"

    implementation 'com.bugsnag:bugsnag-android-ndk:4.+'

    testImplementation 'junit:junit:4.13.2'
}
